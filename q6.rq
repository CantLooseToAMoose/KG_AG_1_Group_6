#We matched some but we are lucky to have so many since we only labelled 250 reviews for a sentiment due to time constraints and yet we matched 6
PREFIX ex:   <http://kg-course/food-nutrition/>
PREFIX sdo:  <https://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>

SELECT
  ?recipe
  ?name
  ?prepTime
  (?sugarContent AS ?sugarContent_grams)
  (COUNT(?review) AS ?positiveReviewCount)
WHERE {
  ?recipe a sdo:Recipe ;
          sdo:recipeCategory ?category ;
          sdo:name ?name .

  # Must be a beverage (case-insensitive match)
  FILTER(CONTAINS(LCASE(?category), "beverage"))

  # OPTIONAL so we don't fail if prepTime is missing
  OPTIONAL { ?recipe sdo:prepTime ?prepTime }

  # Retrieve sugar content from a NutritionInfo node
  ?dish ex:hasRecipe ?recipe ;
        ex:nutrition ?nutri .
  ?nutri sdo:sugarContent ?sugarContent .

  # Count only "positive" reviews with confidence > 0.75
  ?recipe sdo:review ?review .
  ?review sdo:reviewRating ?rating .
  ?rating sdo:ratingValue ?ratingVal ;
          ex:confidencePrediction ?confidence .
  FILTER(?ratingVal = "positive")
  FILTER(?confidence > 0.75)
}
GROUP BY ?recipe ?name ?prepTime ?sugarContent
ORDER BY DESC(?positiveReviewCount)
LIMIT 10
